load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@rules_rust//rust:defs.bzl", "rust_binary")
load("//os:config.bzl", "SECTOR_SIZE", "TOTAL_SECTORS")

rust_binary(
    name = "kernel",
    srcs = ["main.rs"],
    linker_script = "linker.ld",
    platform = "@toolchains_llvm//platforms:linux-x86_32",
    rustc_flags = [
        "-Cpanic=abort",
        "-Clink-arg=-nostdlib",
        "-Clink-arg=-fno-builtin",
        "-Clink-arg=-ffreestanding",
        "-Clink-arg=-Wl,--oformat=binary",
        "-Copt-level=0",
    ],
    tags = ["manual"],
    visibility = ["//os:__pkg__"],
    deps = [
        "//os/builtins",
        "//os/kernel/core",
        "//os/kernel/i686",
    ],
)

expand_template(
    name = "kernel_size",
    out = "kernel_size_test.sh",
    data = [":kernel"],
    is_executable = True,
    substitutions = {
        "{MAX_SIZE}": str(TOTAL_SECTORS * SECTOR_SIZE),
        "{KERNEL}": "$(rootpath :kernel)",
    },
    template = [
        "actual_size=$(wc -c < {KERNEL})",
        "(( actual_size < {MAX_SIZE} )) || exit 1",
    ],
)

sh_test(
    name = "kernel_size_test",
    srcs = [":kernel_size"],
    data = [":kernel"],
)
