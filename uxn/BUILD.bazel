load("@aspect_rules_js//js:defs.bzl", "js_run_devserver")
load("@aspect_rules_js//npm:defs.bzl", "npm_link_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//uxn:http-server/package_json.bzl", http_server_bin = "bin")
load("@uxn//:defs.bzl", "uxn_binary")
load("//tools/uxn5:defs.bzl", "uxn5_format_js")

npm_link_all_packages(name = "node_modules")

npm_link_package(
    name = "node_modules/uxn5",
    src = "@uxn5",
    root_package = package_name(),
)

[
    [
        uxn_binary(
            name = "%s_rom" % tal_file.removesuffix(".tal"),
            srcs = [tal_file],
            out = "%s.rom" % tal_file.removesuffix(".tal"),
            visibility = ["//juxn:__subpackages__"],
        ),
        uxn5_format_js(
            name = "%s_js" % tal_file.removesuffix(".tal"),
            out = "%s.js" % tal_file.removesuffix(".tal"),
            rom = ":%s_rom" % tal_file.removesuffix(".tal"),
        ),
    ]
    for tal_file in glob(["*.tal"])
]

http_server_bin.http_server_binary(
    name = "http_server",
)

js_run_devserver(
    name = "serve",
    args = [package_name()],
    data = [
        "app.js",
        "index.html",
        "style.css",
        ":gol_js",
        ":hello_js",
        ":node_modules/uxn5",
    ],
    tool = ":http_server",
)
